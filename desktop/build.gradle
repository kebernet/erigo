buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath group: 'com.bladecoder.packr', name: 'packr', version: '2.0'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.10"
        classpath 'de.undercouch:gradle-download-task:3.2.0'
    }
}

import com.badlogicgames.packr.*
import de.undercouch.gradle.tasks.download.Download

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "net.ltgt.apt"
apply plugin: 'de.undercouch.download'
apply plugin: 'application'

repositories {
    mavenCentral()
}

configurations {
    provided
}

dependencies {
    compile project(":client-framework")
    compile 'com.intellij:forms_rt:7.0.3'

    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile 'com.squareup.dagger:dagger:1.2.5'
    apt 'com.squareup.dagger:dagger-compiler:1.2.5'

    provided 'com.yuvimasory:orange-extensions:1.3.0'

    // RXTX should be installed in the JRE
    compile group: 'org.rxtx', name: 'rxtx', version: '2.1.7'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets.main.compileClasspath += configurations.provided
sourceSets.test.compileClasspath += configurations.provided
sourceSets.test.runtimeClasspath += configurations.provided

mainClassName = "net.kebernet.configuration.desktop.Erigo"
applicationDefaultJvmArgs = ["-Xmx256m"]

distributions {
    main {
        baseName = "Erigo-universal"
        contents {

        }
    }
}

shadowJar {
    classifier = "all"
    dependencies {
        exclude(dependency(group: 'org.rxtx', name: 'rxtx'))
    }
}

task downloadMacJdk(type: Download){
    src "https://dl.bintray.com/kebernet/generic/mac-oracle-jdk1.8.0_77-rxtx.zip"
    dest rootProject.file("jdks/mac-jdk.zip")
    onlyIfNewer true
}

PackrConfig createBaseConfig(){
    PackrConfig config = new PackrConfig()
    config.executable = "Erigo"
    config.classpath = Arrays.asList(file("build/libs/${project.name}-${project.version}-all.jar").getAbsolutePath())
    config.mainClass = "net.kebernet.configuration.desktop.Erigo"
    config.vmArgs = Arrays.asList("Xmx256m")
    config.minimizeJre = "soft"
    config.resources = [file("src/main/app/logging.properties")]
    return config
}

task packrMac(dependsOn: [downloadMacJdk, shadowJar]) {
    doLast {
        PackrConfig config = createBaseConfig();
        config.platform = PackrConfig.Platform.MacOS
        config.bundleIdentifier = "${mainClassName}"
        config.verbose = true
        config.jdk = file(rootProject.file("jdks/mac-jdk.zip"))
        config.outDir = file("build/packr/mac/Erigo.app")
        new Packr().pack(config)
    }
}

task zipMac(type: Zip, dependsOn: packrMac){
    from file("build/packr/mac/")
    archiveName "Erigo-Mac-${project.version}.zip"
    destinationDir file("build/distributions")
}


build.dependsOn zipMac
build.dependsOn distZip
